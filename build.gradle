apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

description = 'A Java distributed system'

version = 1.5
sourceCompatibility = 1.8
targetCompatibility = 1.8

ant.importBuild 'build.xml'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['workspace/popjava/src']
        }
        resources {
            srcDirs = ['workspace/resources']
        }
    }
    test {
        java {
            srcDirs = ['workspace/popjava/test']
        }
    }
}

dependencies {
    compile group: 'org.javassist',    name: 'javassist',      version: '3.+'
    compile group: 'org.slf4j',        name: 'slf4j-nop',      version: '1.7.+'
    compile group: 'org.yaml',         name: 'snakeyaml',      version: '1.+'
    compile group: 'com.hierynomus',   name: 'sshj',           version: '0.21.1'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.58'
    
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'com.carrotsearch', name: 'junit-benchmarks', version: '0.7.2'
}

test {
    useJUnit()
    
    testLogging.showStandardStreams = true
    
    // use only define suite
    include '**/AllTests.class'
    
    beforeTest { descriptor ->
        logger.lifecycle(descriptor.toString())
    }
}

clean {
    delete 'build'
    delete 'logFolder'
}

task fatJar(type: Jar, group: 'build', description: 'Assembles a jar with all the dependencies') {
    manifest {
        attributes 'Premain-Class': 'popjava.javaagent.POPJavaAgent',
            'Can-Redefine-Classes': true,
            'Can-Retransform-Classes': true,
            'Can-Set-Native-Method-Prefix': true
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    // don't add signed jar checks and tests to jar
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA', 'junit/*', 'testsuite/*'
    with jar

    doLast {
        copy {
            from 'build/libs'
            into 'build'
            include project.name + '-all-' + version + '.jar'
            rename (project.name + '-all-' + version + '.jar', 'popjava.jar')
        }
    }
}

artifacts {
    archives fatJar
}

